allprojects {
    group = 'com.flytrex.ioio'
    version = "1.04"
    ext.versionNamespace = 'ioio'
    ext.versionString = version
}

subprojects {
    apply plugin: 'maven'
    apply plugin: 'signing'

    repositories {
        mavenCentral()
        maven {
            url "http://www.sparetimelabs.com/maven2"
        }
        maven {
            url 'https://oss.sonatype.org/content/groups/staging'
        }
        google()
    }

    afterEvaluate {
        // Replace dependendices on modules from the ioio group with local project
        // dependencies.
        configurations.each { config ->
            def ioioDeps = config.dependencies.findAll { it.group == group }
            config.dependencies.removeAll(ioioDeps)
            ioioDeps.each {
                dependencies.add(config.name, dependencies.project(path: ":$it.name"))
            }
        }

        // Common handling for uploading archives to Maven central.
        if (tasks.findByName('uploadArchives')?.repositories?.findByName('mavenDeployer')) {
            signing {
                sign configurations.archives
            }

        }
    }
}

///////////////////////////////////////////////////////////////////////////////
// Release bundle packaging.

evaluationDependsOnChildren()

def libraries = [
        'IOIOLibCore'            : ['jar'],
        'IOIOLibPC'              : ['jar'],
        'IOIOLibAndroid'         : ['bundleReleaseAar', 'bundleDebugAar'],
        'IOIOLibAndroidBluetooth': ['bundleReleaseAar', 'bundleDebugAar'],
        'IOIOLibAndroidAccessory': ['bundleReleaseAar', 'bundleDebugAar'],
        'IOIOLibAndroidDevice'   : ['bundleReleaseAar', 'bundleDebugAar']
]

def applications = [
        'applications/HelloIOIO'          : ['packageDebug'],
        'applications/HelloIOIOService'   : ['packageDebug'],
        'applications/IOIOSimpleApp'      : ['packageDebug'],
        'applications/pc/HelloIOIOConsole': ['distJar'],
        'applications/pc/HelloIOIOSwing'  : ['distJar'],
        'applications/pc/HelloSequencer'  : ['distJar']
]

def gitFiles(dirs) {
    ('git ls-files -- ' + dirs.join(' ')).execute().text.readLines()
}


task bundle(type: Zip) {
    baseName = 'App'
    version = versionString
    destinationDir = file('../release/software')

    into('src') {
        from fileTree('.').include(gitFiles(libraries.keySet()*.concat('/src'))).exclude('.git*')
        from fileTree('.').include(gitFiles(applications.keySet())).exclude('.git*')
    }

    libraries.each { entry ->
        entry.value.each { value ->
            into('lib') {
                from project(entry.key).tasks[value]
            }
        }
    }

    applications.each { entry ->
        entry.value.each { value ->
            into('bin') {
                from project(entry.key).tasks[value]
            }
        }
    }

    into('doc') {
        from project('IOIOLibCore').tasks.getByName('javadoc')
    }
}
